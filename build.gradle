plugins {
    id 'java'
    id 'java-library'
    id "com.vanniktech.maven.publish" version "0.34.0"
}

group = 'dev.pinter.mailsrs'
version = '0.6'
description = 'An implementation of Sender Rewriting Scheme (SRS)'
repositories {
    mavenCentral()
}

java {
    targetCompatibility = JavaVersion.VERSION_1_8
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'Emerson Pinter'
        )
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << "-Xlint:deprecation"
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.13.4')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

// maven publish configuration
import com.vanniktech.maven.publish.JavaLibrary
import com.vanniktech.maven.publish.JavadocJar
mavenPublishing {
    publishToMavenCentral()

    signAllPublications()

    configure(new JavaLibrary(new JavadocJar.Javadoc(), true))

    coordinates(project.group.toString(), project.name.toString(), project.version.toString())

    pom {
        name = project.name.toString()
        description = project.description.toString()
        inceptionYear = "2025"
        url = "https://github.com/epinter/mail-srs/"
        licenses {
            license {
                name = "The Apache License, Version 2.0"
                url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution = "http://www.apache.org/licenses/LICENSE-2.0.txt"
            }
        }
        developers {
            developer {
                id = "epinter"
                name = "Emerson Pinter"
                url = "https://github.com/epinter/"
            }
        }
        scm {
            url = "https://github.com/epinter/mail-srs/"
            connection = "scm:git:git://github.com/epinter/mail-srs.git"
            developerConnection = "scm:git:ssh://git@github.com/epinter/mail-srs.git"
        }
    }
}